; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %pp-llvm-ver -o %t.pp < %s --llvm-ver %LLVMVER
; RUN: %spirv-ll -a OpenCL -b 64 -c Sampled1D %p/op_image_sample_explicit_lod_1d.spv -o %t
; RUN: %filecheck < %t %t.pp
                      OpCapability Kernel
                      OpCapability Addresses
                      OpCapability Sampled1D
                      OpMemoryModel Physical64 OpenCL
                      OpEntryPoint Kernel %image1d "image1d"
                      OpSource OpenCL_C 102000
                      OpName %in_image "in_image"
                      OpName %in_sampler "in_sampler"

; All types, variables and constants               
            %void_t = OpTypeVoid
           %float_t = OpTypeFloat 32
            %uint_t = OpTypeInt 32 0
         %v4float_t = OpTypeVector %float_t 4
          %v4uint_t = OpTypeVector %uint_t 4
         %sampler_t = OpTypeSampler
      %image1d_t = OpTypeImage %void_t 1D 0 0 0 0 Unknown ReadOnly
           %float_1 = OpConstant %float_t 1
             %int_1 = OpConstant %uint_t 1
   %sampled_image_t = OpTypeSampledImage %image1d_t
      %sampler_fn_t = OpTypeFunction %void_t %sampler_t %image1d_t

; All functions
           %image1d = OpFunction %void_t None %sampler_fn_t
        %in_sampler = OpFunctionParameter %sampler_t
          %in_image = OpFunctionParameter %image1d_t
; CHECK-LT15: %opencl.image1d_t = type opaque       
; CHECK-GE15: define spir_kernel void @image1d(i32 %in_sampler, ptr addrspace(1) %in_image)
; CHECK-LT15: define spir_kernel void @image1d(i32 %in_sampler, %opencl.image1d_t addrspace(1)* %in_image)
                %10 = OpLabel
     %sampled_image = OpSampledImage %sampled_image_t %in_image %in_sampler
                %11 = OpImageSampleExplicitLod %v4float_t %sampled_image %float_1 None
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1d11ocl_samplerf(ptr addrspace(1) %in_image, i32 %in_sampler, float 1.000000e+00)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1d11ocl_samplerf(%opencl.image1d_t addrspace(1)* %in_image, i32 %in_sampler, float 1.000000e+00)
                %12 = OpImageSampleExplicitLod %v4float_t %sampled_image %int_1 None 
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1d11ocl_sampleri(ptr addrspace(1) %in_image, i32 %in_sampler, i32 1)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1d11ocl_sampleri(%opencl.image1d_t addrspace(1)* %in_image, i32 %in_sampler, i32 1)
                %15 = OpImageSampleExplicitLod %v4uint_t %sampled_image %float_1 None
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1d11ocl_samplerf(ptr addrspace(1) %in_image, i32 %in_sampler, float 1.000000e+00)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1d11ocl_samplerf(%opencl.image1d_t addrspace(1)* %in_image, i32 %in_sampler, float 1.000000e+00)
                %16 = OpImageSampleExplicitLod %v4uint_t %sampled_image %int_1 None
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1d11ocl_sampleri(ptr addrspace(1) %in_image, i32 %in_sampler, i32 1)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1d11ocl_sampleri(%opencl.image1d_t addrspace(1)* %in_image, i32 %in_sampler, i32 1)
                      OpReturn
; CHECK: ret void
                      OpFunctionEnd
; CHECK: }

