; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %pp-llvm-ver -o %t.pp < %s --llvm-ver %LLVMVER
; RUN: %spirv-ll -a OpenCL -b 64 %p/op_opencl_remquo_private_v4float.spv -o %t
; RUN: %filecheck < %t %t.pp
                        OpCapability Kernel
                        OpCapability Addresses
                   %1 = OpExtInstImport "OpenCL.std"
                        OpMemoryModel Physical64 OpenCL
                        OpEntryPoint Kernel %remquo_v4float "remquo_v4float"
                        OpSource OpenCL_C 102000

; All types, variables and constants
              %void_t = OpTypeVoid
               %int_t = OpTypeInt 32 1
             %float_t = OpTypeFloat 32
             %v4int_t = OpTypeVector %int_t 4
           %v4float_t = OpTypeVector %float_t 4
         %v4int_ptr_t = OpTypePointer Function %v4int_t
         %remquo_fn_t = OpTypeFunction %void_t
             %float_1 = OpConstant %float_t 1.5
             %float_2 = OpConstant %float_t 2.0
           %v4float_1 = OpConstantComposite %v4float_t %float_1 %float_1 %float_1 %float_1
           %v4float_2 = OpConstantComposite %v4float_t %float_2 %float_2 %float_2 %float_2

; All functions
      %remquo_v4float = OpFunction %void_t None %remquo_fn_t
; CHECK: define spir_kernel void @remquo_v4float()
                   %2 = OpLabel
                   %3 = OpVariable %v4int_ptr_t Function
; CHECK: [[quo:%[a-zA-Z0-9]+]] = alloca <4 x i32>
                   %4 = OpExtInst %v4float_t %1 remquo %v4float_2 %v4float_1 %3
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z6remquoDv4_fS_PDv4_i(<4 x float> <float 2.000000e+00, float 2.000000e+00, float 2.000000e+00, float 2.000000e+00>, <4 x float> <float 1.500000e+00, float 1.500000e+00, float 1.500000e+00, float 1.500000e+00>, ptr [[quo]])
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z6remquoDv4_fS_PDv4_i(<4 x float> <float 2.000000e+00, float 2.000000e+00, float 2.000000e+00, float 2.000000e+00>, <4 x float> <float 1.500000e+00, float 1.500000e+00, float 1.500000e+00, float 1.500000e+00>, <4 x i32>* [[quo]])
                        OpReturn
; CHECK: ret void
                        OpFunctionEnd
; CHECK: }
