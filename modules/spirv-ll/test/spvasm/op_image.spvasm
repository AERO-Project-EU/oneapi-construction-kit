; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %pp-llvm-ver -o %t.pp < %s --llvm-ver %LLVMVER
; RUN: %spirv-ll -a OpenCL -b 64 -c ImageBasic -c Sampled1D %p/op_image.spv -o %t
; RUN: %filecheck < %t %t.pp
                OpCapability Kernel
                OpCapability Addresses
                OpCapability ImageBasic
                OpCapability Sampled1D
                OpMemoryModel Physical64 OpenCL
                OpEntryPoint Kernel %image1d "image1d"
                OpSource OpenCL_C 102000
                OpName %image "image"
      %void_t = OpTypeVoid
      %uint_t = OpTypeInt 32 0
     %float_t = OpTypeFloat 32
     %uint4_t = OpTypeVector %uint_t 4
    %float4_t = OpTypeVector %float_t 4
   %sampler_t = OpTypeSampler
   %image1d_t = OpTypeImage %void_t 1D 0 0 0 0 Unknown ReadOnly
%sampled_image_t = OpTypeSampledImage %image1d_t
       %foo_t = OpTypeFunction %void_t %image1d_t %sampler_t
       %int_0 = OpConstant %uint_t 0
     %image1d = OpFunction %void_t None %foo_t
       %image = OpFunctionParameter %image1d_t
     %sampler = OpFunctionParameter %sampler_t
           %1 = OpLabel
%sampled_image = OpSampledImage %sampled_image_t %image %sampler
   %get_image = OpImage %image1d_t %sampled_image
           %3 = OpImageRead %uint4_t %get_image %int_0
           %4 = OpImageRead %float4_t %get_image %int_0
                OpReturn
                OpFunctionEnd
; CHECK-LT15: %opencl.image1d_t = type opaque
; CHECK-GE15: define spir_kernel void @image1d(ptr addrspace(1) %image, i32{{( %0)?}})
; CHECK-LT15: define spir_kernel void @image1d(%opencl.image1d_t addrspace(1)* %image, i32{{( %0)?}})
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1di(ptr addrspace(1) %image, i32 0)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1di(%opencl.image1d_t addrspace(1)* %image, i32 0)
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1di(ptr addrspace(1) %image, i32 0)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1di(%opencl.image1d_t addrspace(1)* %image, i32 0)
; CHECK: ret void
; CHECK: }
