; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %pp-llvm-ver -o %t.pp < %s --llvm-ver %LLVMVER
; RUN: %spirv-ll -a OpenCL -b 64 -c ImageBasic -c Sampled1D %p/op_image_read_2d_array.spv -o %t
; RUN: %filecheck < %t %t.pp
                      OpCapability Kernel
                      OpCapability Addresses
                      OpCapability ImageBasic
                      OpCapability Sampled1D
                      OpMemoryModel Physical64 OpenCL
                      OpEntryPoint Kernel %image2d_array "image2d_array"
                      OpSource OpenCL_C 102000
                      OpName %image "image"
            %void_t = OpTypeVoid
            %uint_t = OpTypeInt 32 0
           %float_t = OpTypeFloat 32
           %uint4_t = OpTypeVector %uint_t 4
          %float4_t = OpTypeVector %float_t 4
%image2d_array_t = OpTypeImage %void_t 2D 0 1 0 0 Unknown ReadOnly
; CHECK-LT15: %opencl.image2d_array_t = type opaque
             %foo_t = OpTypeFunction %void_t %image2d_array_t
             %int_1 = OpConstant %uint_t 1
            %int4_1 = OpConstantComposite %uint4_t %int_1 %int_1 %int_1 %int_1
     %image2d_array = OpFunction %void_t None %foo_t
             %image = OpFunctionParameter %image2d_array_t
; CHECK-GE15: define spir_kernel void @image2d_array(ptr addrspace(1) %image)
; CHECK-LT15: define spir_kernel void @image2d_array(%opencl.image2d_array_t addrspace(1)* %image)
                 %1 = OpLabel
                 %3 = OpImageRead %uint4_t %image %int4_1
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui16ocl_image2darrayDv4_i(ptr addrspace(1) %image, <4 x i32> <i32 1, i32 1, i32 1, i32 1>)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui16ocl_image2darrayDv4_i(%opencl.image2d_array_t addrspace(1)* %image, <4 x i32> <i32 1, i32 1, i32 1, i32 1>)
                 %4 = OpImageRead %float4_t %image %int4_1
; CHECK-GE15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef16ocl_image2darrayDv4_i(ptr addrspace(1) %image, <4 x i32> <i32 1, i32 1, i32 1, i32 1>)
; CHECK-LT15: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef16ocl_image2darrayDv4_i(%opencl.image2d_array_t addrspace(1)* %image, <4 x i32> <i32 1, i32 1, i32 1, i32 1>)
                      OpReturn
; CHECK: ret void
                      OpFunctionEnd
; CHECK: }
