; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %spirv-ll -a Vulkan %spv_file_s -e SPV_EXT_shader_atomic_float_min_max -c AtomicFloat32MinMaxEXT | %filecheck %s
            OpExtension "SPV_EXT_shader_atomic_float_min_max"
            OpCapability Shader
            OpCapability AtomicFloat32MinMaxEXT
       %1 = OpExtInstImport "GLSL.std.450"
            OpMemoryModel Logical GLSL450
            OpEntryPoint GLCompute %atomic "atomic"
            OpExecutionMode %atomic LocalSize 1 1 1
            OpName %atomic "atomic"
            OpName %a "a"
            OpDecorate %block_t Block
            OpDecorate %block_var DescriptorSet 0
            OpDecorate %block_var Binding 0
    %void = OpTypeVoid
    %uint = OpTypeInt 32 0
 %float_t = OpTypeFloat 32
 %block_t = OpTypeStruct %float_t
%ptr_Uniform_block_t = OpTypePointer Uniform %block_t
%ptr_Uniform_int = OpTypePointer Uniform %float_t
       %3 = OpTypeFunction %void
       %9 = OpConstant %float_t 42.4
  %uint_1 = OpConstant %uint 1
  %uint_0 = OpConstant %uint 0
%block_var = OpVariable %ptr_Uniform_block_t Uniform
  %atomic = OpFunction %void None %3
       %5 = OpLabel
       %a = OpAccessChain %ptr_Uniform_int %block_var %uint_0
%original = OpAtomicFMinEXT %float_t %a %uint_1 %uint_0 %9
            OpReturn
            OpFunctionEnd
; CHECK: ; ModuleID = '{{.*}}'
; CHECK: [[TYPE:%.*]] = type { float }
; CHECK: define spir_kernel void @atomic(ptr addrspace(1){{( %0)?}}, ptr addrspace(1){{( %1)?}})
; CHECK: [[PTR:%[a-zA-Z0-9_]+]] = getelementptr [[TYPE]], ptr addrspace(1) %0, i32 0, i32 0
; CHECK: {{%[a-zA-Z0-9_]+}} = call spir_func float @_Z25atomic_fetch_min_explicitPU3AS1Vff(ptr addrspace(1) [[PTR]], float 0x4045333340000000)
; CHECK: ret void
; CHECK: declare spir_func float @_Z25atomic_fetch_min_explicitPU3AS1Vff(ptr addrspace(1), float)
