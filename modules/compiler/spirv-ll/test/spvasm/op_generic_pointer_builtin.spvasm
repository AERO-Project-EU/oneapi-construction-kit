; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %spirv-ll -a OpenCL -b 64 -c GenericPointer %p/op_generic_pointer_builtin.spv | %filecheck %s

               OpCapability Addresses
               OpCapability Linkage
               OpCapability Kernel
               OpCapability Int64
               OpCapability GenericPointer
               OpMemoryModel Physical64 OpenCL
               OpEntryPoint Kernel %20 "entry_pt"

               OpName %__spirv_BuiltInGlobalInvocationId "__spirv_BuiltInGlobalInvocationId"
               OpDecorate %__spirv_BuiltInGlobalInvocationId LinkageAttributes "__spirv_BuiltInGlobalInvocationId" Import
               OpDecorate %__spirv_BuiltInGlobalInvocationId Constant
               OpDecorate %__spirv_BuiltInGlobalInvocationId BuiltIn GlobalInvocationId
               OpDecorate %__spirv_BuiltInGlobalInvocationId Alignment 32
       %void = OpTypeVoid
      %ulong = OpTypeInt 64 0
    %v3ulong = OpTypeVector %ulong 3
        %_ptr_Generic_v3ulong = OpTypePointer Generic %v3ulong
 %_ptr_CrossWorkgroup_v3ulong = OpTypePointer CrossWorkgroup %v3ulong
%__spirv_BuiltInGlobalInvocationId = OpVariable %_ptr_CrossWorkgroup_v3ulong CrossWorkgroup

         %19 = OpTypeFunction %void
         %20 = OpFunction %void None %19
      %entry = OpLabel

; Check that multiple uses of a BuiltIn cast to a generic pointer don't crash
; the translator.
; CHECK: [[T0:%.*]] = trunc i64 0 to i32
; CHECK: [[ID0:%.*]] = call spir_func i64 @_Z13get_global_idj(i32 [[T0]])
         %51 = OpPtrCastToGeneric %_ptr_Generic_v3ulong %__spirv_BuiltInGlobalInvocationId
         %52 = OpLoad %v3ulong %51 Aligned 32
         %53 = OpCompositeExtract %ulong %52 0

; CHECK: [[T1:%.*]] = trunc i64 0 to i32
; CHECK: [[ID1:%.*]] = call spir_func i64 @_Z13get_global_idj(i32 [[T1]])
        %114 = OpPtrCastToGeneric %_ptr_Generic_v3ulong %__spirv_BuiltInGlobalInvocationId
        %115 = OpLoad %v3ulong %114 Aligned 32
        %116 = OpCompositeExtract %ulong %115 0

               OpReturn
               OpFunctionEnd
