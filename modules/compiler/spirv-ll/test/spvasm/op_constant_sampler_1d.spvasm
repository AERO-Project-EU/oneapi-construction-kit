; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %spirv-ll -a OpenCL -b 64 -c Sampled1D -c LiteralSampler %spv_file_s | %filecheck %s
; CHECK: ; ModuleID = '{{.*}}'
                        OpCapability Kernel
                        OpCapability Addresses
                        OpCapability Sampled1D
                        OpCapability LiteralSampler
                        OpMemoryModel Physical64 OpenCL
                        OpEntryPoint Kernel %constant_sampler "constant_sampler"
                        OpSource OpenCL_C 102000
                        OpName %in_image "in_image"
              %void_t = OpTypeVoid
              %uint_t = OpTypeInt 32 0
             %float_t = OpTypeFloat 32
            %v4uint_t = OpTypeVector %uint_t 4
           %v4float_t = OpTypeVector %float_t 4
           %sampler_t = OpTypeSampler
           %image1d_t = OpTypeImage %void_t 1D 0 0 0 0 Unknown ReadOnly
        %sampler_fn_t = OpTypeFunction %void_t %image1d_t
     %sampled_image_t = OpTypeSampledImage %image1d_t
              %uint_1 = OpConstant %uint_t 1
             %float_1 = OpConstant %float_t 1
       %const_sampler = OpConstantSampler %sampler_t None 0 Nearest
    %constant_sampler = OpFunction %void_t None %sampler_fn_t
            %in_image = OpFunctionParameter %image1d_t
; CHECK: define spir_kernel void @constant_sampler(ptr addrspace(1) %in_image)
                   %1 = OpLabel
       %sampled_image = OpSampledImage %sampled_image_t %in_image %const_sampler
                   %2 = OpImageSampleExplicitLod %v4float_t %sampled_image %float_1 None
; CHECK: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1d11ocl_samplerf(ptr addrspace(1) %in_image, i32 16, float 1.000000e+00)
                   %3 = OpImageSampleExplicitLod %v4float_t %sampled_image %uint_1 None
; CHECK: {{%[a-zA-Z0-9]+}} = call spir_func <4 x float> @_Z11read_imagef11ocl_image1d11ocl_sampleri(ptr addrspace(1) %in_image, i32 16, i32 1)
                   %6 = OpImageSampleExplicitLod %v4uint_t %sampled_image %float_1 None
; CHECK: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1d11ocl_samplerf(ptr addrspace(1) %in_image, i32 16, float 1.000000e+00)
                   %7 = OpImageSampleExplicitLod %v4uint_t %sampled_image %uint_1 None
; CHECK: {{%[a-zA-Z0-9]+}} = call spir_func <4 x i32> @_Z12read_imageui11ocl_image1d11ocl_sampleri(ptr addrspace(1) %in_image, i32 16, i32 1)
                        OpReturn
; CHECK: ret void
                        OpFunctionEnd
; CHECK: }
