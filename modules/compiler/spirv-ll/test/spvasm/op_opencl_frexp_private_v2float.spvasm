; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %spirv-ll -a OpenCL -b 64 %spv_file_s | %filecheck %s
                        OpCapability Kernel
                        OpCapability Addresses
                   %1 = OpExtInstImport "OpenCL.std"
                        OpMemoryModel Physical64 OpenCL
                        OpEntryPoint Kernel %frexp_v2float "frexp_v2float"
                        OpEntryPoint Kernel %frexp_v2float_uint "frexp_v2float_uint"
                        OpSource OpenCL_C 102000

; All types, variables and constants
              %void_t = OpTypeVoid
               %int_t = OpTypeInt 32 1
               %uint_t = OpTypeInt 32 0
             %float_t = OpTypeFloat 32
             %v2int_t = OpTypeVector %int_t 2
             %v2uint_t = OpTypeVector %uint_t 2
           %v2float_t = OpTypeVector %float_t 2
         %v2int_ptr_t = OpTypePointer Function %v2int_t
         %v2uint_ptr_t = OpTypePointer Function %v2uint_t
          %frexp_fn_t = OpTypeFunction %void_t
             %float_1 = OpConstant %float_t 1.4
           %v2float_1 = OpConstantComposite %v2float_t %float_1 %float_1

; All functions
       %frexp_v2float = OpFunction %void_t None %frexp_fn_t
; CHECK: define spir_kernel void @frexp_v2float()
                   %2 = OpLabel
                   %3 = OpVariable %v2int_ptr_t Function
; CHECK: [[ptr:%[a-zA-Z0-9]+]] = alloca <2 x i32>
                   %4 = OpExtInst %v2float_t %1 frexp %v2float_1 %3
; CHECK: {{%[a-zA-Z0-9]+}} = call spir_func <2 x float> @_Z5frexpDv2_fPDv2_i(<2 x float> <float 0x3FF6666660000000, float 0x3FF6666660000000>, ptr [[ptr]])
                        OpReturn
; CHECK: ret void
                        OpFunctionEnd
; CHECK: }

       %frexp_v2float_uint = OpFunction %void_t None %frexp_fn_t
; CHECK: define spir_kernel void @frexp_v2float_uint()
                   %5 = OpLabel
                   %6 = OpVariable %v2uint_ptr_t Function
; CHECK: [[ptr:%[a-zA-Z0-9]+]] = alloca <2 x i32>
                   %7 = OpExtInst %v2float_t %1 frexp %v2float_1 %6
; CHECK: {{%[a-zA-Z0-9]+}} = call spir_func <2 x float> @_Z5frexpDv2_fPDv2_i(<2 x float> <float 0x3FF6666660000000, float 0x3FF6666660000000>, ptr [[ptr]])
                        OpReturn
; CHECK: ret void
                        OpFunctionEnd
; CHECK: }
