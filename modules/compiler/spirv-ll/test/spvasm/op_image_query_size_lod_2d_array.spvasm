; Copyright (C) Codeplay Software Limited. All Rights Reserved.

; RUN: %spirv-ll -a OpenCL -b 64 -c SampledBuffer %p/op_image_query_size_lod_2d_array.spv | %filecheck %s
                        OpCapability Kernel
                        OpCapability Addresses
                        OpCapability SampledBuffer
                        OpMemoryModel Physical64 OpenCL
                        OpEntryPoint Kernel %image2d_array "image2d_array"
                        OpSource OpenCL_C 102000
                        OpName %image "image"

; All types, variables and constants
              %void_t = OpTypeVoid
               %int_t = OpTypeInt 32 1
             %v3int_t = OpTypeVector %int_t 3
     %image2d_array_t = OpTypeImage %void_t 2D 0 1 0 0 Unknown ReadOnly
     %query_size_fn_t = OpTypeFunction %void_t %image2d_array_t
               %int_1 = OpConstant %int_t 1

; All functions
       %image2d_array = OpFunction %void_t None %query_size_fn_t
               %image = OpFunctionParameter %image2d_array_t
; CHECK: define spir_kernel void @image2d_array(ptr addrspace(1) %image)
                   %1 = OpLabel
                   %2 = OpImageQuerySizeLod %v3int_t %image %int_1
; CHECK: [[size_result:%[a-zA-Z0-9]+]] = call spir_func [[bitness:[a-zA-Z0-9]+]] @_Z20get_image_array_size16ocl_image2darray(ptr addrspace(1) %image)
; CHECK: [[size_result_trunc:%[a-zA-Z0-9]+]] = trunc [[bitness]] [[size_result]] to i32
; CHECK: [[v3int_idx_2:%[a-zA-Z0-9]+]] = insertelement <3 x i32> undef, i32 [[size_result_trunc]], i32 2
; CHECK: [[width_result:%[a-zA-Z0-9]+]] = call spir_func i32 @_Z15get_image_width16ocl_image2darray(ptr addrspace(1) %image)
; CHECK: [[v3int_idx_0:%[a-zA-Z0-9]+]] = insertelement <3 x i32> [[v3int_idx_2]], i32 [[width_result]], i32 0
; CHECK: [[height_result:%[a-zA-Z0-9]+]] = call spir_func i32 @_Z16get_image_height16ocl_image2darray(ptr addrspace(1) %image)
; CHECK: {{%[a-zA-Z0-9_]+}} = insertelement <3 x i32> [[v3int_idx_0]], i32 [[height_result]], i32 1
                        OpReturn
; CHECK: ret void
                        OpFunctionEnd
; CHECK: }
