if(NOT DEFINED GENERATE_OUTPUT_FILE)
  message(FATAL_ERROR
    "Required cmake variable GENERATE_OUTPUT_FILE not set!"
  )
endif()

set(generate_input_definitions "")

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/main.in" main)

set(types "half;float;double")
set(nan_types "ushort;uint;ulong")
set(vector_sizes ";2;3;4;8;16")

foreach(iter RANGE 2)
  foreach(vector_size IN LISTS vector_sizes)
    list(GET types ${iter} type)
    set(generate_type "${type}${vector_size}")

    list(GET nan_types ${iter} nan_type)
    set(generate_nan_type "${nan_type}${vector_size}")

    if(${type} STREQUAL "half")
      # Half support is optional and needs hidden behind a macro
      string(CONFIGURE "\n#ifdef __CA_BUILTINS_HALF_SUPPORT\n${main}\n#endif\n"
        processed @ONLY)
    elseif(${type} STREQUAL "double")
      # Double support is optional and needs hidden behind a macro
      string(CONFIGURE "#ifdef __CA_BUILTINS_DOUBLE_SUPPORT\n${main}\n#endif\n"
        processed @ONLY)
    else()
      string(CONFIGURE "${main}" processed @ONLY)
    endif()
    set(generate_input_definitions "${generate_input_definitions}${processed}")
  endforeach()
endforeach()

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/some_scalar.in" some_scalar)

set(vector_sizes "2;3;4;8;16")

foreach(iter RANGE 2)
  foreach(vector_size IN LISTS vector_sizes)
    list(GET types ${iter} type)
    set(generate_type "${type}${vector_size}")

    if(${type} STREQUAL "half")
      # Half support is optional and needs hidden behind a macro
      string(CONFIGURE "\n#ifdef __CA_BUILTINS_HALF_SUPPORT\n${some_scalar}\n#endif\n"
        processed @ONLY)
    elseif(${type} STREQUAL "double")
      # Double support is optional and needs hidden behind a macro
      string(CONFIGURE "#ifdef __CA_BUILTINS_DOUBLE_SUPPORT\n${some_scalar}\n#endif\n"
        processed @ONLY)
    else()
      string(CONFIGURE "${some_scalar}" processed @ONLY)
    endif()
    set(generate_input_definitions "${generate_input_definitions}${processed}")
  endforeach()
endforeach()

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/only_float.in" only_float)

set(vector_sizes ";2;3;4;8;16")

foreach(vector_size IN LISTS vector_sizes)
  set(generate_type "float${vector_size}")

  string(CONFIGURE "${only_float}" processed @ONLY)
  set(generate_input_definitions "${generate_input_definitions}${processed}")
endforeach()

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/abacus_math.in" abacus_math)
string(CONFIGURE "${abacus_math}" abacus_math @ONLY)
file(WRITE "${GENERATE_OUTPUT_FILE}" "${abacus_math}")
